plugins {
    id 'application'

    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'org.jmailen.kotlinter' version '3.0.0'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
}

group 'no.nav.sf.henvendelse.db'
version '0.0.1'

jar {
    manifest {
        attributes 'Main-Class': 'no.nav.sf.henvendelse.db.BootstrapKt'
    }
}

shadowJar {
    archiveBaseName = 'app'
    archiveClassifier = ''
}

application {
    mainClassName = 'no.nav.sf.henvendelse.db.BootstrapKt'
}

repositories {
    mavenCentral()
    jcenter()

    ["maven-release"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubPassword")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or githubPassword")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = 'x-access-token'
                password = token
            }
        }
    }
}

dependencies {
    implementation "no.nav.security:token-validation-core:3.0.0"

    implementation 'com.google.code.gson:gson:2.10'

    implementation 'io.lettuce:lettuce-core:6.5.5.RELEASE'

    implementation "org.http4k:http4k-server-apache:3.251.0"

    implementation "io.github.microutils:kotlin-logging:1.7.9"
    implementation "ch.qos.logback:logback-classic:1.5.13"
    implementation "net.logstash.logback:logstash-logback-encoder:7.4"

    implementation "io.prometheus:simpleclient_common:0.8.1"
    implementation "io.prometheus:simpleclient_hotspot:0.8.1"

    implementation "org.postgresql:postgresql:42.3.9"

    implementation 'org.jetbrains.exposed:exposed-core:0.46.0'
    implementation 'org.jetbrains.exposed:exposed-dao:0.46.0'
    implementation 'org.jetbrains.exposed:exposed-jdbc:0.46.0'
    implementation 'org.jetbrains.exposed:exposed-java-time:0.46.0'

    implementation "com.zaxxer:HikariCP:3.4.1"

    implementation "net.minidev:json-smart:2.4.9" //Desired transient json-smart to token-validation above
    implementation "org.apache.commons:commons-compress:1.26.0" //transient http4k-apache
    implementation "org.apache.httpcomponents:httpclient:4.5.13" //Desired transient httpclient to http4k-apache
    implementation "com.nimbusds:nimbus-jose-jwt:9.37.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'io.mockk:mockk:1.13.8'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = "17"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "17"
    targetCompatibility = "17"
}

task runSyntaxDemo(type: JavaExec) {
    main = 'Syntax_demoKt'
    classpath = sourceSets.test.runtimeClasspath
}